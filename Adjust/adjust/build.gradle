apply plugin: 'com.android.library'

class Globals {
    static String versionName = '4.11.0'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "${Globals.versionName}"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task clearJar(type: Delete) {
    delete "build/outputs/adjust-${Globals.versionName}.jar"
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('build/outputs/')
    include('classes.jar')
    rename ('classes.jar', "adjust-${Globals.versionName}.jar")
}

makeJar.dependsOn(clearJar, build)

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
